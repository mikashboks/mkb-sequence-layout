plugins {
    id("com.android.library")
    id("kotlin-android")
    id("maven-publish")
    id("org.jetbrains.kotlinx.binary-compatibility-validator") version "0.13.2"
}

android {
    compileSdk 34

    defaultConfig {
        minSdkVersion(16)
        targetSdkVersion(34)
        setProperty("archivesBaseName", "com.mikashboks.sequencelayout-${versionName}")
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_11
        targetCompatibility = JavaVersion.VERSION_11
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_11.toString()
    }

    namespace 'com.mikashboks.sequencelayout'
}

ext {
    PUBLISH_GROUP_ID = "com.github.mikashboks"
    PUBLISH_ARTIFACT_ID = "sequence-layout"
    PUBLISH_VERSION = "1.2.1"
}

group = PUBLISH_GROUP_ID
version = PUBLISH_VERSION

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release

                groupId = PUBLISH_GROUP_ID
                artifactId = PUBLISH_ARTIFACT_ID
                version = PUBLISH_VERSION

                pom {
                    name = PUBLISH_ARTIFACT_ID
                    url = "https://github.com/mikashboks/mkb-sequence-layout"
                    licenses {
                        license {
                            name = "The Apache License, Version 2.0"
                            url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                        }
                    }
                    scm {
                        connection = "scm:git:github.com/mikashboks/mkb-sequence-layout.git"
                        developerConnection = "scm:git:ssh://github.com/mikashboks/mkb-sequence-layout.git"
                        url = "https://github.com/mikashboks/mkb-sequence-layout/tree/master"
                    }
                }
            }
        }
    }
}

dependencies {
    implementation("androidx.appcompat:appcompat:1.6.0")
    implementation("androidx.core:core-ktx:1.8.0")
}
